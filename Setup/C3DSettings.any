Main.ModelSetup.C3DFileData = 
{
    AnyString PrefixStr=""; //if the dataset makes use of a prefix name which goes in front of all markers and processed datanames write it here
    #define PrefixDef(Arg) Arg
    FileName = MOCAP_C3D_DATA_PATH +"/"+ Main.ModelSetup.TrialSpecificData.TrialFileName + ".c3d"; //add the extension to the file name here
    ConstructModelOnOff = Off;
    ConstructChartOnOff = Off;
    
    
    ConstructWeightFunUsingResidualOnOff  = On;
    WeightTransitionTime = 0.5;

    SearchAndReplace = {{"Force." ,""},
                    {"Moment." ,""}};

    MarkerUseAllPointsOnOff = On; 
    MarkerUseCamMaskOnOff = Off;
    PointsScaleFactor = 1/1000;
    MarkerDrawOnOff =Off;
    
    AnyInt MarkerFilterOrder = 2;
    AnyInt AnalogFilterOrder = 2;

   // HACK_WORKAROUND. We need to disable filters when 
// using recordings which has less data than 3 times filter order
// This approach assumes a filter order of 2.
    MarkerFilterIndex = iffun(gtfun(Header.LastFrameNo-Header.FirstFrameNo+1,MarkerFilterOrder *3 ), 0 ,-1) ;
    AnalogFilterIndex = iffun(gtfun((Header.LastFrameNo-Header.FirstFrameNo+1)*Header.NoAnalogSamplesPer3DFrame,
                                     AnalogFilterOrder*3 ), 1 ,-1);    

  Filter =  {
    N = .MarkerFilterOrder;
    Fc = {5};
    Type = LowPass;
  };
  
  /// This filter is used for the analog data
  AnyFunButterworthFilter LowPassFilter = {
    FilterForwardBackwardOnOff = On;
    AutomaticInitialConditionOnOff = On;
    N = .AnalogFilterOrder  ;
    AnyVar CutOffFrequency=5 ;
     
    AnyVar SampleFreq=.Header.VideoFrameRate* .Header.NoAnalogSamplesPer3DFrame   ;
    W = {1/(iffun(eqfun(SampleFreq,0),100.0,SampleFreq)*0.5)*CutOffFrequency};
    Type = LowPass;
  };
  
  
  MarkerScaleXYZ = 0.015*{1,1,1};
  MarkerRGB={0,0,1};    

};  